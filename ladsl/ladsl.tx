/*
    Land Administration DSL textX grammar
*/

File:
    transactions*=Transaction
;

Transaction:
    LegalTransaction | GeoTransaction
;

LegalTransaction:
    Transfer | Update | Create
;

GeoTransaction:
    CreateDevSite | ApplyDevSite
;

Transfer:
    'transfer'
    'from'
        _from+=Party[',']
        'parcel' (parcel_all?='all' | parcel=Parcel)
        ('share' from_share+=Share[','])?
    'to'
        to+=Party[',']
        ('share' to_share+=Share[','])?
;

Party: /[0-9a-zA-Z_-]+/;
Parcel: ko=INT '/' parcel=INT ('/' part=INT)?;
Building: ko=INT '/' parcel=INT '/' building=INT ('/' unit=INT)?;
Share: nom=INT '/' den=INT;

Update:
    UpdateParty | UpdateUnit
;

UpdateParty:
    'update'
    'party' party=Party
    (('firstName' first_name=STRING)?
     ('lastName' last_name=STRING)?
     ('name' name=STRING)?
     ('address' address=STRING)?
    )#
;

UpdateUnit:
    'update'
    ('parcel' parcel=Parcel
     | 'building' building=Building)
    ('landUse' land_use=ID
     |'right' right=Right 'party' party=Party)
;

Right:
    Mortgage;

Mortgage: 'mortgage' ammount=FLOAT percent=FLOAT '%';

Create:
    'create'
     'building' building=Building
     'party' party+=Party[',']
     'share' share+=Share[',']
;

CreateDevSite:
    'create' 'development' 'site' site=DevSiteID
    'geoaggregates' geoaggregates+=GeoAggregateID[',']
;

ApplyDevSite:
    'apply' 'development' 'site' site=DevSiteID
;

DevSiteID:
    ko=INT '/' id=INT;

GeoAggregateID:
    ko=INT '/' id=INT;

// special rule for comments.
Comment:
    /\/\/.*$/
;
